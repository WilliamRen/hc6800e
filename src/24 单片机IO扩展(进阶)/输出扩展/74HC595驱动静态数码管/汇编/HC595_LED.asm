;/*******************************************************************************
;*  标题:           试验74HC595驱动数码管上显示数字（汇编语言）                 *
;* 																			    *
;*   连接方法：JP12用条线冒短接  JP3和JP2 用8PIN排线连接					    *													
;********************************************************************************
;* 通过本例程了解 74HC595（串入并出）基本原理和使用  		                    *
;*                         	       										        *
;* 请学员认真消化本例程，懂74C595在汇编语言中的操作                             *
;********************************************************************************/

 ;================================================

     MOSIO    EQU  P3.6   ;串行数据输入
     S_CLK    EQU  P3.4   ;移位时钟脉冲
     R_CLK    EQU  P3.5   ;输出锁存器控制脉冲
     
;================================================

       ORG  0000H
       LJMP  INTI
       ORG  0030H

;================================================
  INTI:
       	MOV 20H,#00H
        MOV A,20H
  MAIN:
	                
	    ANL A,#0FH	    ;屏蔽累加器的高4位
		MOV DPTR,#TAB	;将数据表格的首地址 0100H存入16位的数据地址指针DPTR中
		MOVC A,@A+DPTR	;查表
		MOV R0,A	    
	  	ACALL DELAY	    ;调用延时子程序
		MOV A,20H		;将20H单元的数据传送给累加器
		INC A			;累加器+1
	   ;DA A			;二-十进制调整	  如果这句使用数码管只显示 0-9
		MOV 20H, A		 	
		CALL OUT_595	;调用595子程序
	    CALL  DELAY
		MOV  A ,20H
	    JMP  MAIN
;---------------------------------------------------------------
;输出锁存器输出数据子程序
;---------------------------------------------------------------
OUT_595:
        LCALL WR_595
        CLR  R_CLK
        NOP				 ;用NOP指令产生方波
        NOP
        SETB R_CLK      ;下降沿将数据送到输出锁存器
        RET
;--------------------------------------------------------------
;移位寄存器接收数据子程序
;--------------------------------------------------------------
WR_595:         
        MOV R4,#08H      ;总共移8次   
        MOV A,R0
WR_LOOP:        
        RLC A	  		 ;带进位左移
        MOV MOSIO,C	    
        CLR S_CLK       ;下降沿发生移位
	    NOP
        NOP
      	SETB S_CLK
        DJNZ R4,WR_LOOP
        RET
;--------------------------------------------------------------
;延时子程序
;--------------------------------------------------------------
DELAY:          
	    MOV  R5,#010H
DEL0:         
	    MOV  R6,#0FFH
DEL1:          
	    MOV   R7,#01FH
	    DJNZ  R7,$
	    DJNZ  R6,DEL1
	    DJNZ  R5,DEL0
	    RET
              

	ORG 0100H
TAB:	DB 0C0H,0F9H,0A4H,0B0H,99H,92H,82H,0F8H		;LED字模表
	DB 80H,90H,88H,83H,0C6H,0A1H,86H,08EH
	END

;/此表为 LED 的字模            
; 0    1    2    3    4    5    6   7    8    9    A    b    c    d    E    -    L   P    U    Hidden  _ (20)
;{ 0xC0,0xF9,0xA4,0xB0,0x99,0x92,0x82,0xF8,0x80,0x90,0x88,0x83,0xC6,0xA1,0x86,0xbf,0xc7,0x8c,0xc1, 0xff,  0xf7 }; 


